plugins {
    id 'groovy'
    id 'application'
    id 'jacoco'
    id "org.sonarqube" version "3.3"
}

group 'asciimirror'
version '1.0'

repositories {
    mavenCentral()
    google()
}

dependencies {
    // Spock Framework
    testImplementation platform('org.codehaus.groovy:groovy-bom:3.0.12')
    testImplementation platform('org.spockframework:spock-bom:2.1-groovy-3.0')
    testImplementation 'org.spockframework:spock-core'

    // Spock Reports
    testRuntimeOnly 'com.athaydes:spock-reports:2.3.0-groovy-3.0'
    testImplementation 'org.slf4j:slf4j-api:1.7.36'
    testRuntimeOnly 'org.slf4j:slf4j-simple:1.7.36'
}

// In order to run the application type the command:
// ./gradlew run -q --console=plain --args="ip_addresses"

run {
    standardInput = System.in
}

application {
    mainModule = 'asciimirror.main'
    mainClass = 'asciimirror.Main'
}

jar {
    manifest {
        attributes(
                'Main-Class': 'lv.id.jc.ipcounter.Main'
        )
    }
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport

    systemProperty 'com.athaydes.spockframework.report.showCodeBlocks', true
    systemProperty 'com.athaydes.spockframework.report.projectName', 'ASCII Mirror'
    systemProperty 'com.athaydes.spockframework.report.projectVersion', 1.0
}

jacoco {
    toolVersion = "0.8.7"
}

jacocoTestReport {
    dependsOn test

    reports {
        xml.required = true
        html.required = true
        csv.required = false
    }

    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    "**/config/*"
            ])
        }))
    }
}

